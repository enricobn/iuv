buildscript {
    repositories {
        mavenCentral()
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

plugins {
    id "com.moowork.node"
    id 'org.jetbrains.kotlin.multiplatform'
}

apply plugin: 'maven'
apply plugin: 'kotlinx-serialization'
apply plugin: 'maven-publish'

group 'org.iuv'
version '0.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://kotlin.bintray.com/kotlinx"
    }
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm8') {
            // Configure a single target's compilations (main and test)
            compilations.all {
                tasks[compileKotlinTaskName].kotlinOptions {
                    jvmTarget = '1.8'
                }
            }
        }
        
        fromPreset(presets.js, 'js') {
            // Configure a single target's compilations (main and test)
            compilations.all {
                tasks[compileKotlinTaskName].kotlinOptions {
                    moduleKind = "umd"
                    sourceMap = true
                    metaInfo = true
                    sourceMapEmbedSources = "always"
                }
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
            }
        }
        jvm8Main {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
            }
        }
        jvm8Test {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }
    }
}

node {
    download = true
}

task yarnInstall(type: YarnTask) {
    args = ['install']
}

// START workaround to copy all js files needed for test to build/node_modules

configurations {
    jsLibs
    jsTestLibs
}

dependencies {
    jsLibs "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    jsLibs "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$serialization_version"
    jsTestLibs "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

task populateNodeModules(type: Copy, dependsOn: jsTestClasses) {
    def jsTarget = kotlin.targets.js

    from(jsTarget.compilations.main.output) {
        include '**/*.js'
        // To flatten files
        eachFile { details ->
            details.path = details.name
        }
    }

    from(jsTarget.compilations.test.output) {
        include '**/*.js'
        // To flatten files
        eachFile { details ->
            details.path = details.name
        }
    }

    (configurations.jsLibs + configurations.jsTestLibs).each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }

    into "${buildDir}/node_modules"

    includeEmptyDirs = false
}

// END

task runKarma(type: YarnTask, dependsOn: [populateNodeModules, yarnInstall]) {
    args = ['test']
}

jsTest.dependsOn runKarma

publishToMavenLocal.dependsOn jsTest

configurations {
    compileClasspath
}